buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://cache-redirector.jetbrains.com/intellij-dependencies'}
    }
}

plugins {
    id 'idea'
    id 'org.jetbrains.intellij' version '0.7.3'
    id "org.jetbrains.grammarkit" version "2020.3.2"
	id "org.jetbrains.kotlin.jvm" version "$kotlinVersion"
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://cache-redirector.jetbrains.com/intellij-dependencies'}
}

sourceCompatibility = javaVersion
targetCompatibility = javaVersion
tasks.withType(JavaCompile) {options.encoding = 'UTF-8' }
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions.jvmTarget = "1.8"
}

sourceSets {
    main {
        java.srcDirs 'src/main/gen'
        kotlin.srcDirs 'src/main/kotlin'
        resources.srcDirs 'src/main/resources'
    }
    test {
        kotlin.srcDirs 'src/test/kotlin'
        resources.srcDirs 'src/test/resources'
    }
}
// GRAMMAR KIT
import org.jetbrains.grammarkit.tasks.GenerateLexer
task generateLexer(type: GenerateLexer) {
    source = "src/main/grammar/Purescript.flex"
    targetDir = "src/main/gen/org/purescript/lexer/"
    targetClass = "_PSLexer"
    purgeOldFiles = true
    skeleton = "src/main/grammar/idea-flex.skeleton"
}

compileJava {
    dependsOn generateLexer
}

compileKotlin {
    dependsOn generateLexer
}

test {
     systemProperty "idea.tests.overwrite.data", Boolean.getBoolean("idea.tests.overwrite.data")
}

// Plugin config
version = "${version}"
intellij {
    pluginName 'purity-intellij'
    version ideaVersion

    publishPlugin {
        token = intellijPublishToken
        channels publishChannels
    }

    prepareSandbox {
        copy {
            from './PsiViewer.jar'
            into './build/idea-sandbox/plugins/'
        }
    }

    patchPluginXml {
        sinceBuild '203'
    }

}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
}

// Intellij config
idea {
    project {
        jdkName = javaVersion
        languageLevel = javaVersion
    }
    module {
        generatedSourceDirs += file('src/main/gen')
    }
}

tasks.processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}
