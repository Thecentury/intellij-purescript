buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    }
}

plugins {
    id 'org.jetbrains.intellij' version '0.6.3'
    id "org.jetbrains.grammarkit" version "2020.3.2"
	id "org.jetbrains.kotlin.jvm" version "$kotlinVersion"
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

sourceCompatibility = javaVersion
targetCompatibility = javaVersion
tasks.withType(JavaCompile) {options.encoding = 'UTF-8' }
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions.jvmTarget = "1.8"
}

sourceSets {
    main {
        java.srcDirs 'src/main/java', 'src/main/gen'
        kotlin.srcDirs 'src/main/java'
        resources.srcDirs 'src/main/resources'
    }
    test {
        java.srcDirs 'src/test/java'
        kotlin.srcDirs 'src/test/java'
        resources.srcDirs 'src/test/resources'
    }
}
// GRAMMAR KIT

apply plugin: 'org.jetbrains.grammarkit'
import org.jetbrains.grammarkit.tasks.GenerateLexer
task generateLexer(type: GenerateLexer) {
    source = "src/main/grammar/Purescript.flex"
    targetDir = "src/main/gen/org/purescript/lexer/"
    targetClass = "_PSLexer"
    purgeOldFiles = true
    skeleton = "src/main/grammar/idea-flex.skeleton"
}

compileJava {
    dependsOn generateLexer
}

compileKotlin {
    dependsOn generateLexer
}

test {
     systemProperty "idea.tests.overwrite.data", Boolean.getBoolean("idea.tests.overwrite.data")
}

// take the version number defined in gradle build and use that in plugin.xml
task initConfig(type: Copy) {
    from('src/main/resources') {
        include '**/plugin.xml'
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [version: version])
    }
}

// Plugin config
version = "${version}"
apply plugin: 'org.jetbrains.intellij'
intellij {
    pluginName 'purity-intellij'
    version ideaVersion

    publishPlugin {
        token = intellijPublishToken
        channels publishChannels
    }

    prepareSandbox {
        copy {
            from './PsiViewer.jar'
            into './build/idea-sandbox/plugins/'
        }
    }

    patchPluginXml {
        sinceBuild '203'
    }

}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation 'com.github.ballerina-platform:lsp4intellij:0.94.2'
}

// Intellij config
apply plugin: 'idea'
idea {
    project {
        jdkName = javaVersion
        languageLevel = javaVersion
    }
    module {
        generatedSourceDirs += file('src/main/gen')
    }
}
