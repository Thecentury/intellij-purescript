import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://cache-redirector.jetbrains.com/intellij-dependencies'}
    }
}

plugins {
    id 'org.jetbrains.intellij' version '1.9.0'
    id "org.jetbrains.kotlin.jvm" version "$kotlinVersion"
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://cache-redirector.jetbrains.com/intellij-dependencies'}
}

dependencies {
    implementation project(":lexer")
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    testImplementation(platform("org.junit:junit-bom:5.8.2"))
    testImplementation("org.junit.jupiter:junit-jupiter:5.8.2")
    testImplementation("junit:junit:4.13.2")
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")

}


tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    sourceCompatibility = "11"
    targetCompatibility = "11"
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "11"
    }
}


tasks.named("test").configure {
    systemProperty "idea.tests.overwrite.data", Boolean.getBoolean("idea.tests.overwrite.data")
    useJUnitPlatform()
}

// Plugin config
version = property("version")
intellij {
    pluginName = 'purity-intellij'
    version = ideaVersion

    publishPlugin {
        token = intellijPublishToken
        channels = [publishChannels]
    }

    patchPluginXml {
        sinceBuild = '203'
    }

}

tasks.processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}
